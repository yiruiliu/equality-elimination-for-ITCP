linprogMax := function(obj,A,b,linrows) ## max(obj*x) given Ax<= b, linrows(linrows didn't work) denotes equality constraints, 
	local  s, rlist;
	rlist:=LoadQSLP([],A,b,[],qs_exec);
	s:=rlist[1];
	LoadQSLPobj(s, obj);
	SolveQSLP(s,[]);
	rlist:=GetQSLPsol_primal(s);
	FlushQSLP(s);
	return [rlist[5],rlist[3]]; # [x, max(obj*x)] 
end;

EqualityEliminationDetectfFullDim := function(A,b)
	local f, Anew, acol, AnewT, arow,bnew, LB, I, XFVAL;
	f := ZeroMutable([1..Size(A[1])]);
	Append(f,[1]);
	Anew := ShallowCopy(A);
	acol := ZeroMutable([1..Size(A)]) + 1;
	AnewT := TransposedMat(Anew);
	AnewT := ShallowCopy(AnewT);
	Append(AnewT,[acol]);
	Anew := TransposedMat(AnewT);
	Anew := ShallowCopy(Anew);
	arow := ZeroMutable([1..Size(A[1])]);
	Append(arow,[1]);
	Append(Anew,[arow]);
	bnew := ShallowCopy(b);
	Append(bnew,[1]);
	LB := ZeroMutable([1..Size(A[1])]);
	Append(bnew,LB);
	I := -IdentityMat(Size(Anew[1])){[1..Size(Anew[1]) - 1]};
	Append(Anew, I);
	XFVAL := linprogMax(f,Anew,bnew,[]);
	return[XFVAL[1],XFVAL[2]];
end;

EqualityEliminationEE := function(E1, A)
#eliminate bounded variables Dx'=c from Bx<=b
#where E=[D|-c],A=[B|-b]
	local AA, R, RT, jb, i, col, row, rr, Aa, j, k, c;
	AA := ShallowCopy(A);
	R := RREF(E1);
	RT := TransposedMat(R);
	jb := [];
	for i in [1..Size(RT)] do
		col := RT[i];
		if Maximum(col) = 1 then
			Append(jb,[i]);
		fi;
	od;
	for i in [1..Size(jb)] do
		row := R[i];
		row[jb[i]] := 0;
		rr := Difference([1..Size(row)], Positions(row,0));
		Aa := NullMat(Size(AA),Size(AA[1]));
		for j in [1..Size(rr)] do
			for k in [1..Size(AA)] do
				c := AA[k][jb[i]] * -row[rr[j]];
				Aa[k][rr[j]] := c;
			od;
		od;
		AA := AA + Aa;
	od;
	for k in [1..Size(AA)] do
		for j in [1..Size(jb)] do
			AA[k][jb[j]] := 0;
		od;
	od;
	return AA;
end;

EqualityEliminationDual := function(A, b)
	local zcol, AA, Aprime, aa, bprime, LB, linrows, f, XFVAL;
	zcol := Zero([1..Size(A[1])]);
	AA := ShallowCopy(A);
	Append(AA,[zcol]);
	Aprime := TransposedMat(AA);
	aa := Zero([1..Size(Aprime[1])]) + 1;
	Aprime := ShallowCopy(Aprime);
	Append(Aprime,[aa]);
	bprime := ZeroMutable([1..Size(Aprime)]);
	bprime[Size(bprime)] := 1;
	linrows := [1..Size(bprime)];
	Append(Aprime,-Aprime);
	Append(bprime,-bprime);
	LB := -IdentityMat(Size(Aprime[1]));
	Append(Aprime,LB);
	Append(bprime,Zero([1..Size(LB)]));
	f := ShallowCopy(b);
	Append(f,[1]);
	XFVAL := linprogMax(-f,Aprime,bprime,[]);
	return [XFVAL[1],XFVAL[2]];
end;

Permute_matrix_parallel_reverse:=function(B,A) #subfunction
# sort columns of matrix B in parallel with indices A and then reverse columns of B
	local Bt,Bf,A1;
	Bt:=TransposedMat(B);
	Bt:=ShallowCopy(Bt);
	A1:=ShallowCopy(A);
	SortParallel(A1,Bt);
	Bf:=Reversed(Bt);
	Bf:=TransposedMat(Bf);
	return [Bf,A1];
end;



Permute_marginalAndJoin:=function(numofVar,listofedge) #subfunction
####example listofedge:=[[1],[2],[3],[4],[5],[6],[3,6],[5,6]]; permute column indices of A such that the first 7 indices are [1],..[6],[3,6],[5,6]
	local A,Len_LOE,i;
	Len_LOE:=Size(listofedge);
	A:=[Len_LOE+1..Len_LOE+2^(numofVar)-1];

	for i in [1..Len_LOE] do
		A[set2int(listofedge[i])]:=i;
	od;
	return A;
end;

EqualityEliminationinitialize := function(Equal,c,Amatrix,b,numofVar,listofedge)
####example listofedge:=[[1],[2],[3],[4],[5],[6],[3,6],[5,6]]; numofVar := 6;
	local A, EqualpA, Equalp, AmatrixpA, Amatrixp,EqualpT, AmatrixpT;
	A := Permute_marginalAndJoin(numofVar,listofedge);
	EqualpA := Permute_matrix_parallel_reverse(Equal, A);
	Equalp := EqualpA[1];
	AmatrixpA := Permute_matrix_parallel_reverse(Amatrix, A);
	Amatrixp := AmatrixpA[1];
	EqualpT := TransposedMat(Equalp);
	EqualpT := ShallowCopy(EqualpT);
	Append(EqualpT,[-c]);
	AmatrixpT := TransposedMat(Amatrixp);
	AmatrixpT := ShallowCopy(AmatrixpT);
	Append(AmatrixpT,[-b]);
	return [TransposedMat(EqualpT), TransposedMat(AmatrixpT)];
end;

AppendColumn:=function(A,column)   ##subfunction
	local AT, A1;
	AT:=TransposedMat(A);
	AT:=ShallowCopy(AT);
	Append(AT,[column]);
	A1:=TransposedMat(AT);
	return ShallowCopy(A1);
end;

AddconstraintsTopAndBottom_indep:=function(ncinstance) ##subfunction
	local linrows,i,ShOB1,con,j,conlin;
	ShOB1 := GenShannonUnBounded( ncinstance[3] );
	i := Size( ShOB1[1] ) + 1;
	linrows := [  ];
	for con  in ncinstance[1]  do     #### H(Out(t),In(t))=H(In(t))
		conlin := ZeroMutable( [ 1 .. 2 ^ ncinstance[3] - 1 ] );
		conlin[set2int( con[1] )] := 1;
		conlin[set2int( con[2] )] := -1;
		Append( ShOB1[1], [ conlin ] );
		Append( ShOB1[2], [ 0 ] );
		Append( linrows, [ i ] );
		i := i + 1;
	od;
	conlin := ZeroMutable( [ 1 .. 2 ^ ncinstance[3] - 1 ] );
	if ncinstance[2]>1 then
		for j  in [ 1 .. ncinstance[2] ]  do    #### H(s1,s2,..,sj)=H(s1)+H(s2)+,...,+H(sj)
			conlin[set2int( [ j ] )] := 1;
		od;
		conlin[set2int( [ 1 .. ncinstance[2] ] )] := -1;
		Append( ShOB1[1], [ conlin ] );
		Append( ShOB1[2], [ 0 ] );
		Append( linrows, [ i ] );
		i := i + 1;
	fi;
	return [ShOB1[1],ShOB1[2],linrows];
end;

AddconstraintsTopAndBottom_noindep:=function(ncinstance) ##subfunction
	local linrows,i,ShOB1,con,j,conlin;
	ShOB1 := GenShannonUnBounded( ncinstance[3] );
	i := Size( ShOB1[1] ) + 1;
	linrows := [  ];
	for con  in ncinstance[1]  do     #### H(Out(t),In(t))=H(In(t))
		conlin := ZeroMutable( [ 1 .. 2 ^ ncinstance[3] - 1 ] );
		conlin[set2int( con[1] )] := 1;
		conlin[set2int( con[2] )] := -1;
		Append( ShOB1[1], [ conlin ] );
		Append( ShOB1[2], [ 0 ] );
		Append( linrows, [ i ] );
		i := i + 1;
	od;
	conlin := ZeroMutable( [ 1 .. 2 ^ ncinstance[3] - 1 ] );
	return [ShOB1[1],ShOB1[2],linrows];
end;

EqualityEliminationMain := function(Equal,c,Amatrix,b,numofVar,listofedge)
####example  listofedge:=[[1],[2],[3],[4],[5],[6],[3,6],[5,6]]; numofVar := 6;
	local AllEqual, EA, Equalp_c, Amatrixp_b, FVAL, A1, A1T, b1, XFVAL, X,
	X1FVAL1, X1, FVAL1, AA1, i, AA1T;
	AllEqual := [];
	EA := EqualityEliminationinitialize(Equal,c,Amatrix,b,numofVar,listofedge);
	Equalp_c := EA[1];
	Amatrixp_b := EA[2];
	FVAL := 0;
	Append(AllEqual, Equalp_c);
	while FVAL = 0 do
		A1 := EqualityEliminationEE(Equalp_c, Amatrixp_b);
		A1T := TransposedMat(A1);
		A1T := ShallowCopy(A1T);
		b1 := - A1T[Size(A1T)];
		A1T := A1T{[1..Size(A1T) - 1]};
		A1 := TransposedMat(A1T);
		A1 := ShallowCopy(A1);
		XFVAL := EqualityEliminationDetectfFullDim(A1, b1);
		#X := XFVAL[1];
		FVAL := XFVAL[2];
		if FVAL = 0 then
			X1FVAL1 := EqualityEliminationDual(A1, b1);
			X1 := X1FVAL1[1];
			FVAL1 := X1FVAL1[2];
			AA1 := AppendColumn(A1, -b1);
			Equalp_c := [];
			Amatrixp_b := [];
			for i in [1..Size(AA1)] do
				if X1[i] > 0 then
					Append(Equalp_c, [AA1[i]]);
				else
					Append(Amatrixp_b, [AA1[i]]);
				fi;
			od;
			Append(AllEqual,Equalp_c);
		fi;
		
	od;
	AA1T := [];
	A1T := TransposedMat(A1);
	for i in [1..Size(A1T)] do
		if Maximum(A1T[i]) = 0 and Minimum(A1T[i]) = 0 then
		
		else
			Append(AA1T,[A1T[i]]);
		fi;
	od;
	AA1 := TransposedMat(AA1T);
	# The last N (N denotes the dimension of the projected region) columns of AA1 are [[5,6],[3,6],[6],[5],[4],[3],[2],[1]]
	return [AA1,AllEqual];
end;

EqualityEliminationFromNet_indep := function(ncinstance,listofedge)
	local SHL, ShOB1,ShOB2,linrows,Equal,diff,Amatrix,c,b,A1AllEqual;
	SHL := AddconstraintsTopAndBottom_indep(ncinstance);
	ShOB1 := SHL[1];
	ShOB2 := SHL[2];
	linrows := SHL[3];
	Equal := ShOB1{linrows};
	diff := Difference([1..Size(ShOB1)],linrows);
	Amatrix := ShOB1{diff};
	c := ZeroMutable([1..length(linrows)]);
	b := ShOB2{diff};
	A1AllEqual := EqualityEliminationMain(Equal,c,Amatrix,b,listofedge);
end;
